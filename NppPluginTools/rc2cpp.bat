@echo off
echo ###########################################################
echo ###      _      __  ___                   __            ###
echo ###     ^| ^| /^| / / / _ \__ _____  ___ ___/ /__  ___     ###
echo ###     ^| ^|/ ^|/ / / // / // / _ \(_-^</ _  / _ \/ _ \    ###
echo ###     ^|__/^|__(_)____/\_,_/_//_/___/\_,_/\___/_//_/    ###
echo ###                                                     ###
echo ###                                          %~nx0 ###
echo ###                                    %~t0 ###
echo ###                                                     ###
echo ### @brief Generate Config CPP from Resource File...    ###
echo ###                                                     ###
echo ### @see %~n0.readme.txt for details.                 ###
echo ###                                                     ###
echo ### @param %%1 - Resource file                           ###
echo ###                                                     ###
echo ###########################################################
if [%1] equ [] goto :ERROR_NO_RC
if [%~x1] neq [.rc] goto :ERROR_NO_RC
set CPPEXTN=cpp
set HPPEXTN=hpp
set CPPOUT=%~p1\%~n1.%CPPEXTN%
set TITLE=%~p1
set TTLBEG=10
setlocal EnableDelayedExpansion
:TTLBEGLPP
if "!TITLE:~-%TTLBEG%,9!"=="NppPlugin" goto :GOTTTLBEG
set /a TTLBEG+=1
goto :TTLBEGLPP
:GOTTTLBEG
set /a TTLBEG-=9
set TTLEND=%TTLBEG%
:TTLENDLPP
if "!TITLE:~-%TTLEND%,1!"=="\" goto :GOTTTLEND
set /a TTLEND-=1
goto :TTLENDLPP
:GOTTTLEND
set /a TTLEND-=1
if not "%TTLEND%"=="0" (set PROJECT=!TITLE:~-%TTLEND%,-1!
) else set PROJECT=
set /a TTLEND+=1
set TITLE=!TITLE:~-%TTLBEG%,-%TTLEND%!
endlocal & set TITLE=%TITLE:_= %& set PROJECT=%PROJECT%
set IDDCNT=0
echo ### ///////////////////////////////////////////////////@file %~n1.%CPPEXTN%
echo ///////////////////////////////////////////////////@file %~n1.%CPPEXTN%> "%CPPOUT%"
echo ### ///
echo ///>> "%CPPOUT%"
echo ### /// Notepad++ Plugin Configuration.
echo /// Notepad++ Plugin Configuration.>> "%CPPOUT%"
echo ### ///
echo ///>> "%CPPOUT%"
echo ### /// Generated by: %~nx0 from %~nx1
echo /// Generated by: %~nx0 from %~nx1>> "%CPPOUT%"
echo ### ///
echo ///>> "%CPPOUT%"
echo ### ///                                                                %date%
echo ///                                                                %date%>> "%CPPOUT%"
echo ### ///
echo ///>> "%CPPOUT%"
echo ### //////////////////////////////////////////////////////////////////////////////
echo //////////////////////////////////////////////////////////////////////////////>> "%CPPOUT%"
echo ### #include ^<string.h^>
echo #include ^<string.h^>>> "%CPPOUT%"
echo ### #include ^<windows.h^>
echo #include ^<windows.h^>>> "%CPPOUT%"
echo ### #include "%~n1.%HPPEXTN%"
echo #include "%~n1.%HPPEXTN%" >> "%CPPOUT%"
echo ### #include "SettingsDlg.h"
echo #include "SettingsDlg.h" >> "%CPPOUT%"
echo ### TCHAR NPP_PLUGIN_NAME[] = TEXT("%TITLE%");
echo TCHAR NPP_PLUGIN_NAME[] = TEXT("%TITLE%"); >> "%CPPOUT%"
if "%PROJECT%"=="" (echo ### TCHAR NPP_PROJECT_NAME[] = TEXT(""^)^;
) else echo ### TCHAR NPP_PROJECT_NAME[] = TEXT("%PROJECT%");
if "%PROJECT%"=="" (echo TCHAR NPP_PROJECT_NAME[] = TEXT(""^)^; >> "%CPPOUT%"
) else echo TCHAR NPP_PROJECT_NAME[] = TEXT("%PROJECT%"); >> "%CPPOUT%"
setlocal EnableDelayedExpansion
for /f "usebackq tokens=1,2,3,4,5,6,7,8,9 eol=/" %%a in (%1) do call :GENERATE_CPP %%a %%b %%c %%d %%e %%f %%g %%h %%i
echo ###    { -1, -1, -1, NULL, NULL, NULL }
echo     { -1, -1, -1, NULL, NULL, NULL }>> "%CPPOUT%"
echo ### };
echo }; >> "%CPPOUT%"
echo ### const int nbFunc = %IDDCNT%;
echo const int nbFunc = %IDDCNT%; >> "%CPPOUT%"
echo ### FuncItem funcItem[nbFunc];
echo FuncItem funcItem[nbFunc]; >> "%CPPOUT%"
echo ### ENVSET envsetlst[] =
echo ENVSET envsetlst[] = >> "%CPPOUT%"
echo ### {
echo { >> "%CPPOUT%"
set IDDINC=0
:IDDCNT
set IDDVAR=!IDDVAR[%IDDINC%]!
set IDDVAR=%IDDVAR: =_%
set IDDVAR=%IDDVAR:"=%
call :TOUPPER IDDVAR
echo ###    { TEXT(!IDDVAR[%IDDINC%]!), SettingsDlg(IDD_%IDDVAR%, ENVVAR_%IDDVAR%, TEXT(!IDDVAR[%IDDINC%]!)) },
echo     { TEXT(!IDDVAR[%IDDINC%]!), SettingsDlg(IDD_%IDDVAR%, ENVVAR_%IDDVAR%, TEXT(!IDDVAR[%IDDINC%]!)) }, >> "%CPPOUT%"
set /a IDDINC+=1
if "%IDDINC%" neq "%IDDCNT%" goto :IDDCNT
endlocal
echo ### };
echo }; >> "%CPPOUT%"
goto :EOF
:GENERATE_CPP
    if "%1" equ "CAPTION" goto :CAPTION
    if "%1" equ "COMBOBOX" goto :COMBOBOX
    exit /b 0
:CAPTION
    set IDDVAR=%2
    set IDDVAR[%IDDCNT%]=%IDDVAR%
    set IDDVAR=%IDDVAR: =_%
    set IDDVAR=%IDDVAR:"=%
    call :TOUPPER IDDVAR
    if "%IDDCNT%" equ "0" goto :CAPTION1
    echo ###    { -1, -1, -1, NULL, NULL, NULL }
    echo     { -1, -1, -1, NULL, NULL, NULL }>> "%CPPOUT%"
    echo ### };
    echo }; >> "%CPPOUT%"
:CAPTION1
    echo ### ENVVAR ENVVAR_%IDDVAR%[] =
    echo ENVVAR ENVVAR_%IDDVAR%[] =>> "%CPPOUT%"
    echo ### {
    echo { >> "%CPPOUT%"
    set /a IDDCNT+=1
    set ENVVAR=""
    exit /b
:COMBOBOX
    set ENVVAR=%2
    set ENVBEG=11
    set ENVDEX=
    if "%ENVVAR:~-6,2%" equ "__" call :ENVDEX
    if "%ENVVAR:~-4,1%" equ "C" goto :CBOX
    if "%ENVVAR:~-4,1%" equ "D" goto :DBOX
    if "%ENVVAR:~-4,1%" equ "F" goto :FBOX
    exit /b
:ENVDEX
    set /a ENVBEG-=3
:ENVDEXLPP
    if "!ENVVAR:~-%ENVBEG%,1!"=="_" goto :ENVDEXBEG
    set /a ENVBEG+=1
    goto :ENVDEXLPP
:ENVDEXBEG
    set /a ENVBEG-=1
    set ENVDEX=[!ENVVAR:~-%ENVBEG%,-6!]
    set /a ENVBEG+=7
    exit /b 0
:CBOX
    echo ###    { %ENVVAR%, -1, -1, "!ENVVAR:~-%ENVBEG%,6!%ENVDEX%", NULL, NULL },
    echo     { %ENVVAR%, -1, -1, "!ENVVAR:~-%ENVBEG%,6!%ENVDEX%", NULL, NULL },>> "%CPPOUT%"
    exit /b
:DBOX
    echo ###    { %ENVVAR%, -1, %ENVVAR:DBOX=BTTN%, "!ENVVAR:~-%ENVBEG%,6!%ENVDEX%", NULL, "!ENVVAR:~-%ENVBEG%,3!%DRV%ENVDEX%" },
    echo     { %ENVVAR%, %ENVVAR:DBOX=BTTN%, -1, "!ENVVAR:~-%ENVBEG%,6!%ENVDEX%", NULL, "!ENVVAR:~-%ENVBEG%,3!DRV%ENVDEX%" },>> "%CPPOUT%"
    exit /b
:FBOX
    echo ###    { %ENVVAR%, %ENVVAR:FBOX=BTTN%, -1, "!ENVVAR:~-%ENVBEG%,6!%ENVDEX%", NULL, NULL },
    echo     { %ENVVAR%, -1, %ENVVAR:FBOX=BTTN%, "!ENVVAR:~-%ENVBEG%,6!%ENVDEX%", NULL, NULL },>> "%CPPOUT%"
    exit /b
:TOUPPER
    for %%i in ("a=A" "b=B" "c=C" "d=D" "e=E" "f=F" "g=G" "h=H" "i=I" "j=J" "k=K" "l=L" "m=M" "n=N" "o=O" "p=P" "q=Q" "r=R" "s=S" "t=T" "u=U" "v=V" "w=W" "x=X" "y=Y" "z=Z") do call set "%~1=%%%~1:%%~i%%"
    exit /b
:ERROR_NO_RC
echo ###
echo ###                                                   No Resource File...
echo ###
