@echo off
echo ###########################################################
echo ###      _      __  ___                   __            ###
echo ###     ^| ^| /^| / / / _ \__ _____  ___ ___/ /__  ___     ###
echo ###     ^| ^|/ ^|/ / / // / // / _ \(_-^</ _  / _ \/ _ \    ###
echo ###     ^|__/^|__(_)____/\_,_/_//_/___/\_,_/\___/_//_/    ###
echo ###                                                     ###
echo ###                                          %~nx0 ###
echo ###                                    %~t0 ###
echo ###                                                     ###
echo ### @brief Generate Resource File from Config LST...    ###
echo ###                                                     ###
echo ### @see %~n0.readme.txt for details.                 ###
echo ###                                                     ###
echo ### @param %%1 - LST file                                ###
echo ###                                                     ###
echo ###########################################################
if [%1] equ [] goto :ERROR_NO_LST
if [%~x1] neq [.lst] goto :ERROR_NO_LST
set NPPPLUGIN=NppPlugin
set RESOURCE=Resource
set RCEXTN=rc
set HPPEXTN=hpp
set TITLE=%~n1
set TITLE=%TITLE:NppPlugin=%
set TITLE=%TITLE:Resource=%
set TITLE=%TITLE:_= %
set RCOUT=%~p1\%NPPPLUGIN%%RESOURCE%.%RCEXTN%
echo ### ///////////////////////////////////////////////////@file %NPPPLUGIN%%RESOURCE%.%RCEXTN%
echo ////////////////////////////////////@file %NPPPLUGIN%%RESOURCE%.%RCEXTN%> "%RCOUT%"
echo ### ///
echo ///>> "%RCOUT%"
echo ### /// Notepad++ Plugin %TITLE% Configuration.
echo /// Notepad++ Plugin %TITLE% Configuration.>> "%RCOUT%"
echo ### ///
echo ///>> "%RCOUT%"
echo ### /// Generated by: %~nx0 from %~nx1
echo /// Generated by: %~nx0 from %~nx1>> "%RCOUT%"
echo ### ///
echo ///>> "%RCOUT%"
echo ### ///                                                                %date%
echo ///                                                                %date%>> "%RCOUT%"
echo ### ///
echo ///>> "%RCOUT%"
echo ### //////////////////////////////////////////////////////////////////////////////
echo //////////////////////////////////////////////////////////////////////////////>> "%RCOUT%"
echo ### #include ^<string.h^>
echo #include ^<string.h^>>> "%RCOUT%"
echo ### #include ^<windows.h^>
echo #include ^<windows.h^>>> "%RCOUT%"
echo ### #include "%NPPPLUGIN%%RESOURCE%.%HPPEXTN%"
echo #include "%NPPPLUGIN%%RESOURCE%.%HPPEXTN%">> "%RCOUT%"
set YINC=20
set XSPC=10
set WCBX=300
set WCBM=140
set DCBX=120
set XCBX=100
set XTXT=20
set WCHR=18
set HCHR=9
set WTXT=80
set XOKP=200
set XCNP=240
set XBTN=400
set WSBN=40
set WBTN=20
set HBTN=13
set XDLG=40
set YDLG=40
set WDLG=500
set HDLG=400
set IDPX=ID
set TBTN="&..."
set TBOK="&OK"
set TBCN="&CANCEL"
set DIALOG=DIALOG
set TEXT=TEXT
set BTTN=BTTN
set CBOX=CBOX
set DBOX=DBOX
set FBOX=FBOX
set TBOX=TBOX
set TBX2=TBX2
set TBX3=TBX3
set YPOS=%YINC%
set YEND=%YINC%
for /f "usebackq tokens=1,2,3,4,5,6,7,8,9,10 delims=, eol=#" %%a in (%1) do call :GENERATE_RC %%a %%b %%c %%d %%e %%f %%g %%h %%i %%j
call :DIALOG_END
goto :EOF
:GENERATE_RC
if "%~1" equ "%DIALOG%" ( goto :DIALOG
) else if "%~1" equ "%TEXT%" ( goto :TEXT
) else if "%~1" equ "%CBOX%" ( goto :CBOX
) else if "%~1" equ "%DBOX%" ( goto :DBOX
) else if "%~1" equ "%FBOX%" ( goto :FBOX
) else if "%~1" equ "%TBOX%" ( goto :TBOX
) else if "%~1" equ "%TBX2%" ( goto :TBX2
) else if "%~1" equ "%TBX3%" goto :TBX3
exit /b 0
:DIALOG
if "%YPOS%" neq "%YINC%" call :DIALOG_END
set DIALOG_ID=%2
set DIALOG_ID=%DIALOG_ID: =_%
set DIALOG_ID=IDD_%DIALOG_ID:"=%
echo %DIALOG_ID% DIALOGEX %XDLG% %YDLG% %WDLG% %HDLG%>> "%RCOUT%"
echo ### %DIALOG_ID% DIALOGEX %XDLG% %YDLG% %WDLG% %HDLG%
echo STYLE DS_SETFONT ^| DS_FIXEDSYS ^| WS_POPUP ^| WS_CAPTION ^| WS_SYSMENU>> "%RCOUT%"
echo ### STYLE DS_SETFONT ^| DS_FIXEDSYS ^| WS_POPUP ^| WS_CAPTION ^| WS_SYSMENU
echo EXSTYLE WS_EX_TOOLWINDOW ^| WS_EX_WINDOWEDGE>> "%RCOUT%"
echo ### EXSTYLE WS_EX_TOOLWINDOW ^| WS_EX_WINDOWEDGE
echo CAPTION %2>> "%RCOUT%"
echo ### CAPTION %2
echo FONT %HCHR%, "MS Sans Serif", 0, 0, 0x0>> "%RCOUT%"
echo ### FONT %HCHR%, "MS Sans Serif", 0, 0, 0x0
echo BEGIN>> "%RCOUT%"
echo ### BEGIN
set /a YPOS+=%YINC%
exit /b
:DIALOG_END
call :BTTN K, O, %TBOK%, %XOKP%, %YPOS%, %WSBN%, %HBTN%
call :BTTN CEL, CAN, %TBCN%, %XCNP%, %YPOS%, %WSBN%, %HBTN%
echo END>> "%RCOUT%"
echo ### END
set YPOS=%YINC%
exit /b 0
:DBOX
call :TEXT _%TEXT% _%2 %3 %XTXT% %YPOS% %WTXT% %HCHR%
call :CBOX _%DBOX% _%2 %XCBX% %YPOS% %WCBX% %DCBX%
call :BTTN _%BTTN% _%2 %TBTN% %XBTN% %YPOS% %WBTN% %HBTN%
set /a YPOS+=%YINC%
exit /b
:FBOX
if "%4" equ "" goto :FBOX_AUTO
set /a YEND=%4+3
set /a YEND*=%YINC%
call :TEXT _%TEXT% _%2 %3 %XTXT% %YEND% %WTXT% %HCHR%
call :CBOX _%FBOX% _%2 %XCBX% %YEND% %WCBX% %DCBX%
call :BTTN _%BTTN% _%2 %TBTN% %XBTN% %YEND% %WBTN% %HBTN%
exit /b
:FBOX_AUTO
call :TEXT _%TEXT% _%2 %3 %XTXT% %YPOS% %WTXT% %HCHR%
call :CBOX _%FBOX% _%2 %XCBX% %YPOS% %WCBX% %DCBX%
call :BTTN _%BTTN% _%2 %TBTN% %XBTN% %YPOS% %WBTN% %HBTN%
set /a YPOS+=%YINC%
exit /b
:TBOX
call :TEXT _%TEXT% _%2 %3 %XTXT% %YPOS% %WTXT% %HCHR%
call :CBOX _%CBOX% _%2 %XCBX% %YPOS% %WCBX% %DCBX%
set /a YPOS+=%YINC%
exit /b
:TBX2
set TBXN=2
goto TBXX
:TBX3
set TBXN=3
:TBXX
shift
call :XXXX %TEXT% %TBXN% %1 %2 %3 %4 %5 %6
set /a YPOS+=%YINC%
set /a YVAR=1
:TBXX_LOOP
set /a XPOS=%XCBX%-%XSPC%
call :TEXT _%TEXT% _%1%YVAR% "%YVAR%:" %XPOS% %YPOS% %WCHR% %HCHR%
call :XXXX %CBOX% %TBXN% %1 %3 %5
set /a YPOS+=%YINC%
set /a YVAR+=1
if "%YPOS%" neq "%YEND%" goto :TBXX_LOOP
set /a YPOS+=%YINC%
exit /b
:XXXX
if "%1" equ "%CBOX%" ( set ARGS=_%1 _%%1%%YVAR%% %%XPOS%% %%YPOS%% %%WCBM%% %%DCBX%%
) else if "%1" equ "%TEXT%" ( set ARGS=_%1 _%%1 %%2 %%XPOS%% %%YPOS%% %%WCBM%% %%HCHR%%
) else exit /b
set SUBR=%1
set /a XNUM=%2
set /a XPOS=%XCBX%
set /a XCNT=1
shift
shift
:XXXX_LOOP
call :%SUBR% %ARGS%
if "%XCNT%" equ "%XNUM%" exit /b
set /a XCNT+=1
set /a XPOS=%XPOS%+%WCBM%+%XSPC%
if "%SUBR%" equ "%TEXT%" shift
shift
goto :XXXX_LOOP
:TEXT
echo LTEXT           %3, %IDPX%%2%1, %4, %5, %6, %7 1>> "%RCOUT%"
echo ### LTEXT           %3, %IDPX%%2%1, %4, %5, %6, %7
exit /b
:CBOX
echo COMBOBOX        %IDPX%%2%1, %3, %4, %5, %6, CBS_DROPDOWN>> "%RCOUT%"
echo ### COMBOBOX        %IDPX%%2%1, %3, %4, %5, %6, CBS_DROPDOWN
exit /b
:BTTN
echo DEFPUSHBUTTON   %3, %IDPX%%2%1, %4, %5, %6, %7, BS_NOTIFY>> "%RCOUT%"
echo ### DEFPUSHBUTTON   %3, %IDPX%%2%1, %4, %5, %6, %7, BS_NOTIFY
exit /b
:ERROR_NO_LST
echo ###
echo ###                                                 Error No List File...
echo ###
goto :EOF
